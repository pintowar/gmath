plugins {
    id 'groovy'
    id 'net.saliman.cobertura' version '2.3.0'
    id 'com.jfrog.bintray' version '1.6'
    id 'maven-publish'
    id 'com.github.tkruse.groovysh' version '1.0.8'
}

group = 'com.github.pintowar'
version = '0.1.2'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile( 'com.athaydes:spock-reports:1.2.11' ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    testCompile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    scm {
        url 'https://github.com/pintowar/gmath'
        connection 'scm:https://github.com/pintowar/gmath.git'
        developerConnection 'scm:git://github.com/pintowar/gmath.git'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'pintowar'
            name 'Thiago Oliveira'
            email 'pintowar@gmail.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'gmath')
                root.appendNode('description', 'An additional math package for groovy.')
                root.appendNode('url', 'http://github.com/pintowar/gmath/')
                root.appendNode('inceptionYear', '2016')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_api_key
    publications = ['mavenJava'] // When uploading Maven-based publication files

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Library for Groovy that mount queries with a simple configuration.'
        websiteUrl = 'https://github.com/pintowar/gmath'
        issueTrackerUrl = 'https://github.com/pintowar/gmath/issues'
        vcsUrl = 'https://github.com/pintowar/gmath.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'math', 'rational']
        publicDownloadNumbers = true
        version {
            name = project.version
            //Optional configuration for GPG signing
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = key_phrase //Optional. The passphrase for GPG signing'
            }
        }
    }
}

bintrayUpload.dependsOn(check)